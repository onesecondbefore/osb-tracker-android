apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

def gitCommitIdAbbrev = 'git rev-parse --verify --short HEAD'.execute().text.trim()
def versionName = "7.5.1"

android {
    compileSdk 34

    defaultConfig {
        minSdk 19
        targetSdkVersion 34

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField "String", "gitCommitIdAbbrev", "\"${gitCommitIdAbbrev}\""
        buildConfigField "String", "versionName", "\"${versionName}\""
    }

    buildTypes {
        release {
//            minifyEnabled true
//            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    namespace 'com.onesecondbefore.tracker'
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

}

dependencies {
    //implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation "androidx.lifecycle:lifecycle-process:2.7.0"
    implementation "androidx.lifecycle:lifecycle-common:2.7.0"
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'com.android.volley:volley:1.2.1'

    implementation 'com.google.android.gms:play-services-location:21.3.0'
    implementation 'com.google.android.gms:play-services-ads-identifier:18.1.0'
    implementation 'com.squareup.okhttp3:okhttp:4.9.0'
    implementation 'com.google.guava:guava:33.0.0-android'

    implementation 'com.iabtcf:iabtcf-decoder:2.0.10'
    
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

afterEvaluate {
    publishing {
        publications {
            publication(MavenPublication) {
                groupId 'com.onesecondbefore.tracker'
                version '7.5.1'
                artifactId 'tracker-android'
                artifact("$buildDir/outputs/aar/osb-release.aar")


                description 'OSB Tracker for Android'
                pom.packaging 'aar'
                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')
                    configurations.implementation.allDependencies.each {
                        println "dependency=$it"
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}