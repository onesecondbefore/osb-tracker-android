apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

def gitCommitIdAbbrev = 'git rev-parse --verify --short HEAD'.execute().text.trim()

android {
    compileSdkVersion 33
    buildToolsVersion '33.0.2'

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
         buildConfigField "String", "gitCommitIdAbbrev", "\"${gitCommitIdAbbrev}\""
    }

    buildTypes {
        release {
//            minifyEnabled true
//            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    namespace 'com.onesecondbefore.tracker'
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

}

dependencies {
    //implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.appcompat:appcompat:1.6.1'

    implementation "androidx.lifecycle:lifecycle-process:2.6.1"
    implementation "androidx.lifecycle:lifecycle-common:2.6.1"

    implementation 'com.google.android.gms:play-services-location:21.0.1'
    implementation 'com.google.android.gms:play-services-ads-identifier:18.0.1'
    implementation 'com.squareup.okhttp3:okhttp:4.9.0'
    
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

publishing {
    publications {
        publication(MavenPublication) {
            groupId 'com.onesecondbefore.tracker'
            version '6.1'
            artifactId 'tracker-android'
            artifact("$buildDir/outputs/aar/osb-release.aar")

            description 'OSB Tracker for Android'
            pom.packaging 'aar'
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.implementation.allDependencies.each {
                    println "dependency=$it"
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }
        }
    }
}